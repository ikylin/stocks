

MA1:MA(CLOSE,5) DOTLINE COLORWHITE;
MA2:MA(CLOSE,30) DOTLINE COLORYELLOW;

YS:MYSKDJ.YST#DAY COLORRED NODRAW;

{参数}
CO:=REFV(O,N);
CH:=REFV(H,N);
CL:=REFV(L,N);
CC:=REFV(C,N);


{基础指标}
ST:=ABS(CO-CC);{实体大小}
YX:=ABS(MIN(CO,CC)-CL);{下影线长度}
CXYX:=(ST*2)<YX;{长下影线}
YANGX:=CO<CC;{阳线}
YINGX:=CO>CC;{阴线}
JDSYX:=((CH-MAX(CO,CC))*5)<ST;{极短上影线}
JDXYX:=((MIN(CO,CC)-CL)*5)<ST;{极短下影线}
JSX:=ABS(CC-CO)>(REFV(CC,1)*0.03) AND ABS(CO-CC)>((CH-CL)*0.6);{坚实线}
JSYANGX:=YANGX AND JDSYX AND JDXYX AND JSX;{坚实阳线}
JSYINGX:=YINGX AND JDSYX AND JDXYX AND JSX;{坚实阴线}
STZZ:=(CO+CC)*0.5;{实体正中}
YANGTYING:=CO<REFV(CO,1) AND CC>REFV(CO,1);{阳吞阴}
YINGTYANG:=CO>REFV(CO,1) AND CC<REFV(CO,1);{阴吞阳}
XX:=ABS(CC-CO)<((CH-CL)*0.5);{星线}
SZX:=ABS(CO-CC)<((CH-CL)*0.05);{十字星}
STKC:=MIN(CC,CO)>REFV(CC,1);{向上跳空收盘价}
STKH:=MIN(CO,CC)>REFV(CH,1);{向上跳空最高价}
XTKC:=MAX(CO,CC)<REFV(CC,1);{向下跳空收盘价}
BH:=CH<REFV(CH,1) AND CL>REFV(CL,1);{包含前一天价格}
PT:=ABS(CH-REFV(CH,1))<((CH-CL)*0.005);{和前一天拥有同样的最高价}
PD:=ABS(CL-REFV(CL,1))<((CH-CL)*0.005);{和前一天拥有同样的最低价}
PH:=ABS(CH-MAX(CC,CO))<((CH-CL)*0.005);{平头线}
PL:=ABS(MIN(CC,CO)-CL)<((CH-CL)*0.005);{平底线}
LZWY:=CO>REFV(CO,1) AND CC<REFV(CC,1);{阴线实体包含前一天实体}
YINGYANGX:=(REFV(YINGX,1) AND YANGX) OR (REFV(YANGX,1) AND YINGX);{一根阴线一根阳线}
YH:=ABS(CC-REFV(CC,1))<((CH-CL)*0.05);{和前一天收盘价接近}
FS:=ABS(CO-REFV(CO,1))<((CH-CL)*0.05);{与昨天开盘价接近}
KFX:=(CH-CL)>(REFV(CC,1)*0.06);{价格跨度大}
JZ:=ABS(((CO+CC)/2)-((CH+CL)/2))<((CH-CL)*0.05);{实体居与价格范围中间}
DB:=ABS(((CO+CC)/2)-CL)<((CH-CL)*0.05);{实体位于底部}


{强关联指标}
YINGTM:=CO>REFV(CC,1) AND CC<REFV(STZZ,1) AND JDSYX AND JDXYX;{阴线吞没阳线一半以上}
YANGTM:=CO<REFV(CC,1) AND CC>REFV(STZZ,1) AND JDSYX AND JDXYX;{阳线吞没阴线一半以上}
SRSTD:=CC>REFV(STZZ,2);{深入前天实体一半以上}
SRSTK:=CC<REFV(STZZ,2);{深入前天实体一半以上}
STKH2:=CL>REFV(CH,2) AND REFV(CL,1)>REFV(CH,2);{最近两天价格向上跳空前天的最高价}
XTKL2:=CH<REFV(CL,2) AND REFV(CH,1)<REFV(CL,2);{最近两天价格向下跳空前天的最低价}
BH3:=REFV(CH,1)<REFV(CH,4) AND REFV(CL,1)>REFV(CL,4)  AND  REFV(CH,1)<REFV(CH,4) AND REFV(CL,1)>REFV(CL,4) AND REFV(CH,1)<REFV(CH,4) AND REFV(CL,1)>REFV(CL,4);{前三天包含在4天前价格范围内}
STKC4:=CO>REFV(CC,1) AND CC>REFV(CC,4);{开盘上跳空昨天收盘价同时收盘上跳空4天前收盘价}
XTKC4:=CO<REFV(CC,1) AND CC<REFV(CC,4);{开盘下跳空昨天收盘价同时收盘下跳空4天前收盘价}


{信号指标}
C001:=CXYX AND JDSYX NODRAW;{锤子线}
C002:=REFV(JSYANGX,1) AND YINGTM NODRAW;{乌云盖顶}
C003:=REFV(JSYINGX,1) AND YANGTM NODRAW;{刺绣}
C004:=REFV(YINGX,1) AND YANGX AND JSX AND YANGTYING NODRAW;{阳吞阴}
C005:=REFV(YANGX,1) AND YINGX AND JSX AND YINGTYANG NODRAW;{阴吞阳}
C006:=REFV(YINGX,2) AND REFV(JSX,2) AND REFV(XX,1) AND YANGX AND JSX AND SRSTD NODRAW;{启明星}
C007:=REFV(YANGX,2) AND REFV(JSX,2) AND REFV(XX,1) AND REFV(STKC,1) AND YINGX AND JSX AND SRSTK NODRAW;{黄昏星}
C008:=REFV(YANGX,1) AND XX AND JDXYX AND STKH NODRAW;{流星线}
C009:=REFV(YINGX,1) AND REFV(JSX,1) AND XX AND JDXYX AND XTKC NODRAW;{倒锤子线}
C010:=REFV(JSX,1) AND XX AND BH NODRAW;{孕线}
C011:=PT OR PD NODRAW;{平头}
C012:=JSX AND PH AND YINGX NODRAW;{捉腰带线K}
C024:=JSX AND PL AND YANGX NODRAW;{捉腰带线D}
C013:=REFV(YINGX,1) AND YINGX AND LZWY NODRAW;{两只乌鸦}
C014:=REFV(YINGX,2) AND REFV(YINGX,1) AND REFV(LZWY,1) AND YINGX AND LZWY NODRAW;{三只乌鸦}
C015:=YINGYANGX AND REFV(JSX,1) AND JSX AND YH NODRAW;{约会线}
C016:=YINGYANGX AND (STKH2 OR XTKL2) NODRAW;{跳空阴阳线}
C017:=REFV(YANGX,4) AND REFV(JSX,4) AND REFV(YINGX,3) AND REFV(YINGX,2) AND REFV(YINGX,1) AND BH3 AND YANGX AND JSX AND STKC4 NODRAW;{上升三法}
C018:=REFV(YINGX,4) AND REFV(JSX,4) AND REFV(YANGX,3) AND REFV(YANGX,2) AND REFV(YANGX,1) AND BH3 AND YINGX AND JSX AND XTKC4 NODRAW;{下降三法}
C019:=REFV(YANGX,2) AND REFV(YANGX,1) AND YANGX NODRAW;{白三兵}
C020:=YINGYANGX AND REFV(JSX,1) AND JSX AND FS NODRAW;{分手线}
C021:=KFX AND SZX AND JZ NODRAW;{黄包车夫线}
C022:=KFX AND XX AND DB NODRAW;{墓碑线}
C023:=REFV(XX,2) AND REFV(XX,1) AND XX NODRAW;{三星线}


{绘图}

{
DRAWTEXT(C001,H*1.01,'锤子线');
DRAWTEXT(C002,H*1.02,'乌云盖顶');
DRAWTEXT(C003,H*1.03,'刺绣');
}
DRAWTEXT(C004,H*1.01,'阳吞阴') COLORRED;
DRAWTEXT(C005,H*1.02,'阴吞阳') COLORGREEN;
DRAWTEXT(C006,H*1.03,'启明星') COLORYELLOW;
DRAWTEXT(C007,H*1.04,'黄昏星') COLORGREEN;
{
DRAWTEXT(C008,H*1.08,'流星线');
DRAWTEXT(C009,H*1.09,'倒锤子线');
DRAWTEXT(C010,H*1.10,'孕线');
DRAWTEXT(C011,H*1.11,'平头');
}
DRAWTEXT(C012,H*1.05,'捉腰带线K') COLORGREEN;
DRAWTEXT(C024,H*1.06,'捉腰带线D') COLORRED;
{
DRAWTEXT(C013,H*1.13,'两只乌鸦');
DRAWTEXT(C014,H*1.14,'三只乌鸦');
DRAWTEXT(C015,H*1.15,'约会线');
DRAWTEXT(C016,H*1.16,'跳空阴阳线');

DRAWTEXT(C017,H*1.07,'上升三法');
DRAWTEXT(C018,H*1.08,'下降三法');

DRAWTEXT(C019 AND NOT(REFV(C019,1)),H*1.19,'白三兵');

DRAWTEXT(C020,H*1.09,'分手线');


DRAWTEXT(C021,H*1.10,'黄包车夫线');
DRAWTEXT(C022,H*1.11,'墓碑线');

DRAWTEXT(C023 AND NOT(REFV(C023,1)),H*1.12,'三星线');
};


BU:=CROSS(HIGH,SAR(3,1,20));
SEL:=CROSS(SAR(3,1,20),LOW);
DRAWTEXT(BU,LOW,'B'),COLORYELLOW,LINETHICK2;
DRAWTEXT(SEL,1.01*HIGH,'S'),COLORGREEN,LINETHICK2;
BJS:BARSLAST(BU) NODRAW;


CSYX:=CLOSE>OPEN AND (HIGH-CLOSE)>((CLOSE-OPEN)*1.7);
DRAWICON(CSYX,H,37);





{STICKLINE(REFX(CROSS(MOD(MONTH,3),0.5),1),CONST(HHV(H,120)),CONST(LLV(L,120)),0,-1);};
